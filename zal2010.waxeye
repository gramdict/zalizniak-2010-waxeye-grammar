entry <- ?HomoNum headword ?StressInfo ?ws defs

StressInfo <- ?ws (NoStress | NormallyNoStress)

NoStress         <- :'(_без удар._)'
NormallyNoStress <- :'(_нормально без удар._)'

defs <= def *(:';' ?ws def)

HomoNum <- ((Number '-' Number) | Number) :'/'

Number <= +[0-9]

headword <- (LETTER *(LETTER | STRESS_MARK | '-'))

def <- symbols ?comma ?ws ?multiword_index
        ?comma
        ?extras ?irregular_forms
        ?alt_lemma
        ?comment ?idioms
        

multiword_index <= index_variants *(plus index_variants)

index_variants <= index_node *(:'//' index_node)

plus <: ?ws '+' ?ws 

idioms <- ?ws :'✧' ?ws idiom *(:';' ?ws idiom)

idiom  <= +(LETTER | STRESS_MARK | [- (),] )

irregular_forms <- ?ws :'△' +irregular_form

irregular_form  <= irregular_plural_gen

irregular_plural_gen <- ?ws :'_Р. мн._' ?ws forms

forms <= form *(?ws :'//' ?ws form) 

form <- ?style headword

style <- (old | folksy) ?ws

old <- :'_устар._'
folksy <- :'_нар.-поэт._'

alt_lemma <- ?ws :'[//' ?style :'__' headword :'__' ?(ws def) :']'

comma <: ',' ?ws

extras <= extra *(comma extra)

extra <=  difficult_plural 
        | difficult_plural_genitive
        | yo

difficult_plural_genitive <- :'_Р. мн. затрудн._'
difficult_plural <- :'_мн. затрудн._'
        | :'_мн. избегается из-за совпадений с_ раб'
yo <- :'ё'

index_node <= angle_index | index0 | index

index0 <- :'0'

index <- index_number ?'*' scheme ?'—' ?circledNumbers

circledNumbers <- +circledNumberOpt

circledNumberOpt <- circledNumber | circledNumberOptional

circledNumberOptional <- :'[' circledNumber :']'

circledNumber <- [①②③④⑤⑥⑦⑧⑨]

angle_index <- (:'<' symbol ?ws index :'>') | (symbol ?ws index)

symbols <= (symbol *(:'//' symbol)) | '§1' | '§2'

symbol <- 'межд.' | 'союз' | 'част.' | 'предик.' 
        | 'мн. _от_' | 'мн.' 
        | 'мо-жо' | 'мо' | 'м' | 'жо' | 'ж' | 'со' | 'с'
        | 'п' | 'н'

index_number <= Number

scheme <- [a-f] ?"'" ?"'"

comment <: ?ws '(' *(comment | !')' . ) ')'

ws <: +[ \t]

LETTER <= [а-яА-ЯёЁ]

STRESS_MARK <= [\u{0300}\u{0301}]

NewLine <: '\r\n' | '\n' | '\r'
