entry <- ?HomoNum headword ?StressInfo ?ws defs ?ws

StressInfo <- ?ws (NoStress | NormallyNoStress | OftenNoStress)

NoStress         <- :'(_без удар._)'
NormallyNoStress <- :'(_нормально без удар._)'
OftenNoStress    <- :'(_часто без удар._)'

defs <= def *(:';' ?ws def)

HomoNum <- ((Number '-' Number) | Number) :'/'

Number <= +[0-9]

headword <- (LETTER *(LETTER | STRESS_MARK | '-'))

def <- symbols ?' нп' ?comma ?ws ?multiword_index
        ?comma
        ?comment
        ?extras
        ?irregular_forms
        ?alt_entries
        ?opposite_aspects
        ?comment ?idioms
        ?examples
        ?see_also

opposite_aspects <- ?ws :'◑' opposite_aspect *(?ws +(((:'//') | (:';')) ?ws) ?style opposite_aspect)

opposite_aspect <- !'св' !'нсв' headword | ((Number | RomanNumber) *(:'//' (Number | RomanNumber)) ?comma ?opposite_aspect_word)

RomanNumber <- 'I' ?'I' ?'I'

opposite_aspect_word <- :'(' ?'-' headword :')'

see_also <- :';' ?ws :'_см. также' :?' отдельно' :'_' ?ws headword

examples <- :':' ?ws example *(:';' ?ws example)

example  <= +(LETTER | STRESS_MARK | [- (),_./—?] | '[' | ']' )

multiword_index <= index_variants *(plus index_variants)

index_variants <= index_node *(:'//' ?style index_node)

plus <: ?ws '+' ?ws 

idioms <- ?ws :'✧' ?ws idiom *(:';' ws idiom)

idiom  <= !long_symbol +(LETTER | STRESS_MARK | [- (),_./—] | '[' | ']' )

irregular_forms <- ?ws :'△' ?ws irregular_form *(comma irregular_form)

irregular_form  <- case ?ws forms

case <- plural_genitive
      | genitive
      | short_masculine
      | passive_participle
      | present_first_person_singular
      | third_person_plural
      | comparative

plural_genitive <- :'_Р. мн._'
genitive        <- :'_Р._'
short_masculine <- :'_кф м_'
passive_participle <- :'_прич. страд._'
present_first_person_singular <- :'_наст. 1 ед._'
third_person_plural <- :'_3 мн._'
comparative <- :'_сравн._'

forms <= form *(?ws :'//' ?ws form)

form <- ?style ?(:'(_затрудн._)' ?ws) headword

style <- (old | folksy | prof | local | official | vernacular) ?ws

old <- :'_устар._'
folksy <- :'_нар.-поэт._'
prof <- :'_проф._'
local <- :'_местн._'
official <- :'_офиц._'
vernacular <- :'_простореч._'

alt_entries <- ?ws :'[' ?:'//' alt_entry *(?ws :'//' alt_entry) ?ws ?:'=' :']'

alt_entry <- ?style ?:'__' ?HomoNum headword ?:'__' ?(ws def) ?see

see <- ?ws :'(_см._)'

comma <: ',' ?ws

extras <= extra *(comma extra)

extra <=  difficult_plural 
        | difficult_plural_genitive
        | difficult_comparative
        | difficult_short_feminine
        | difficult_short_except_masculine
        | difficult_imperative
        | no_plural_genitive
        | passive_participle_zhd
        | yo
        | paragraph
        | sec_case
        | :'[' sec_case :']'
        | impersonal

passive_participle_zhd <- :'_прич. страд._ -жд-'

sec_case <- [ПР] '2' ?preps

preps <- :'(' prep ?(:',' ?ws prep) :')'
prep <- 'в' | 'на'

paragraph <- :'§' Number

no_plural_genitive        <- :'_Р. мн. нет_'
difficult_plural_genitive <- :'_Р. мн. затрудн._' | :'÷'
difficult_plural          <- :'_мн. затрудн._'
                           | :'_мн. избегается из-за совпадений с_ раб'
difficult_short_feminine  <- :'_кф ж затрудн._'
difficult_short_except_masculine <- :'_кф затрудн._ (_кроме кф м_)'
difficult_comparative     <- :'_сравн. затрудн._'
difficult_imperative      <- :'_повел. затрудн._'
impersonal                <- :'безл.'
yo <- :'ё'

index_node <= angle_index | index0 | index

index0 <- :'0'

index <- index_number ?'°' ?'*' scheme
        ?:' ~ 0'
        ?circledNumbers
        ?ws
        ?'⌧' ?'—' ?'✕' ?'~' ?'÷'
        ?letter_seq

letter_seq <- ?ws :'(-' +LETTER :'-)'

circledNumbers <- +circledNumberOpt

circledNumberOpt <- circledNumber | circledNumberOptional

circledNumberOptional <- :'[' circledNumber :']'

circledNumber <- [①②③④⑤⑥⑦⑧⑨]

angle_index <- (:'<' symbol ?comma ?ws index :'>') | (symbol ?ws index)

symbols <= (symbol *(:'//' symbol)) | '§1' | '§2'

long_symbol <= 'межд.' | 'союз' | 'част.' | 'предик.' 
        | 'мн. _от_' | 'мн.' 
        | 'мо⁺' | 'жо⁺'
        | 'ф.'
        | 'мс-п'
        | 'мс'
        | 'сравн.'
        | 'вводн.'
        | 'св-нсв' | 'св' | 'нсв' 
        | 'мо-жо' | 'мо' | 'жо'
        | 'предл.'

symbol <- long_symbol
         | 'со' | 'м' | 'ж' | 'п' | 'н' | 'с'

index_number <= Number

scheme <- [a-f] ?"'" ?"'" ?slash_scheme

slash_scheme <- :'/' [a-c] ?"'" ?"'"

comment <: ?ws '(' *(comment | !')' . ) ')'

ws <: +[ \t]

LETTER <= [а-яА-ЯёЁ]

STRESS_MARK <= [\u{0300}\u{0301}]

NewLine <: '\r\n' | '\n' | '\r'
