entry <- ?HomoNum headword ?StressInfo ?ws defs ?ws

StressInfo <- ?ws (NoStress | NormallyNoStress)

NoStress         <- :'(_без удар._)'
NormallyNoStress <- :'(_нормально без удар._)'

defs <= def *(:';' ?ws def)

HomoNum <- ((Number '-' Number) | Number) :'/'

Number <= +[0-9]

headword <- (LETTER *(LETTER | STRESS_MARK | '-'))

def <- symbols ?comma ?ws ?multiword_index
        ?comma
        ?comment
        ?extras ?irregular_forms
        ?opposite_aspects
        ?alt_entries
        ?comment ?idioms
        ?examples
        ?see_also

opposite_aspects <- ?ws :'◑' opposite_aspect *(:'//' opposite_aspect)

opposite_aspect <- (Number | RomanNumber) ?opposite_aspect_word

RomanNumber <- 'I' ?'I' ?'I'

opposite_aspect_word <- :'(' ?'-' headword :')'

see_also <- :';' ?ws :'_см. также_' ?ws headword

examples <- :':' ?ws example *(:';' ?ws example)

example  <= +(LETTER | STRESS_MARK | [- (),_./—?] | '[' | ']' )

multiword_index <= index_variants *(plus index_variants)

index_variants <= index_node *(:'//' ?style index_node)

plus <: ?ws '+' ?ws 

idioms <- ?ws :'✧' ?ws idiom *(:';' ?ws idiom)

idiom  <= +(LETTER | STRESS_MARK | [- (),_./] | '[' | ']' )

irregular_forms <- ?ws :'△' +irregular_form

irregular_form  <- ?ws case ?ws forms

case <- plural_genitive
      | genitive

plural_genitive <- :'_Р. мн._'
genitive        <- :'_Р._'

forms <= form *(?ws :'//' ?ws form)

form <- ?style headword

style <- (old | folksy | prof) ?ws

old <- :'_устар._'
folksy <- :'_нар.-поэт._'
prof <- :'_проф._'

alt_entries <- ?ws :'[' ?:'//' alt_entry *(?ws :'//' alt_entry) :']'

alt_entry <- ?style :'__' ?HomoNum headword :'__' ?(ws def) ?see

see <- ?ws :'(_см._)'

comma <: ',' ?ws

extras <= extra *(comma extra)

extra <=  difficult_plural 
        | difficult_plural_genitive
        | no_plural_genitive
        | yo
        | paragraph

paragraph <- :'§' Number

no_plural_genitive        <- :'_Р. мн. нет_'
difficult_plural_genitive <- :'_Р. мн. затрудн._' | :'÷'
difficult_plural          <- :'_мн. затрудн._'
                           | :'_мн. избегается из-за совпадений с_ раб'
yo <- :'ё'

index_node <= angle_index | index0 | index

index0 <- :'0'

index <- index_number ?'*' scheme ?circledNumbers ?'—' ?'✕' ?'~'

circledNumbers <- +circledNumberOpt

circledNumberOpt <- circledNumber | circledNumberOptional

circledNumberOptional <- :'[' circledNumber :']'

circledNumber <- [①②③④⑤⑥⑦⑧⑨]

angle_index <- (:'<' symbol ?ws index :'>') | (symbol ?ws index)

symbols <= (symbol *(:'//' symbol)) | '§1' | '§2'

symbol <- 'межд.' | 'союз' | 'част.' | 'предик.' 
        | 'мн. _от_' | 'мн.' 
        | 'мо⁺' | 'жо⁺'
        | 'ф.'
        | 'мс'
        | 'сравн.'
        | 'вводн.'
        | 'св' | 'св' | 'св-нсв' 
        | 'мо-жо' | 'мо' | 'м' | 'жо' | 'ж' | 'со' | 'с'
        | 'предл.'
        | 'п' | 'н'

index_number <= Number

scheme <- [a-f] ?"'" ?"'" ?slash_scheme

slash_scheme <- :'/' [a-c] ?"'" ?"'"

comment <: ?ws '(' *(comment | !')' . ) ')'

ws <: +[ \t]

LETTER <= [а-яА-ЯёЁ]

STRESS_MARK <= [\u{0300}\u{0301}]

NewLine <: '\r\n' | '\n' | '\r'
