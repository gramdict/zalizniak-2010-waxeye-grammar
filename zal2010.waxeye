entry <- ?HomoNum headword ?Unstressed ?ws defs ?ws

Unstressed <- ?ws (AlwaysUnstressed | NormallyUnstressed | OftenUnstressed)

AlwaysUnstressed   <- :'(_без удар._)'
NormallyUnstressed <- :'(_нормально без удар._)'
OftenUnstressed    <- :'(_часто без удар._)'

defs <= def *(:';' ?ws def)

HomoNum <- ((Number '-' Number) | Number) :'/'

Number <= +[0-9]

headword <- (LETTER *(LETTER | STRESS_MARK | '-'))

plural_of <- ?ws :'_от_' ?ws
        ?(?HomoNum headword) ?ws (see | def)

def <- symbols ?' нп'
        ?plural_of
        ?alt_entries
        ?comment
        ?comma ?ws
        ?ref
        ?multiword_index
        ?comment
        ?comma
        ?extras
        ?stylistic_variant
        ?alt_entries
        ?comment
        ?irregular_forms
        ?explicit_forms
        ?(:comma ?:ws paragraph)
        ?alt_infinitive
        ?opposite_aspects
        ?comment
        ?idioms
        ?examples
        ?see_also
        ?alt_entries

alt_infinitive <- ?ws :'[' alt_infinitive_internal :']'
alt_infinitive_internal <=
        (:'_инф. также_' ?ws headword)
        | (:'//__' headword :'__')

ref <-  ('_спряж. см._' | '_склон. см._')
        ?ws
        ?HomoNum
        headword
        ?ws ?comment
        ?comma
        ?'_но без нсв_'

opposite_aspects <- ?ws :'◑' opposite_aspect *(?ws +((:'//' | ';') ?ws) ?style opposite_aspect)

opposite_aspect <- !'св' !'нсв' headword | ((Number | RomanNumber) *(:'//' (Number | RomanNumber)) ?comma ?opposite_aspect_word)

RomanNumber <- 'I' ?'I' ?'I'

opposite_aspect_word <- :'(' ?'-' headword :')'

see_also <- :';' ?ws :'_см. также' :?' отдельно' :'_' ?ws headword

examples <- :':' ?ws example *(:';' ?ws example)

example  <= +(LETTER | STRESS_MARK | [- (),_./—?] | '[' | ']' )

multiword_index <= index_variants *(plus index_variants)

index_variants <= index_node *(:'//' ?style index_node)

plus <: ?ws '+' ?ws 

idioms <- ?ws :'✧' ?ws idiom *(semicolon idiom)

idiom  <= !long_symbol +(LETTER | STRESS_MARK | [- (),_./—:] | '[' | ']' )

semicolon <: ';' ?ws

irregular_forms <- ?ws :('△' | ':') explicit_forms

explicit_forms <- ?ws irregular_form *((comma | semicolon) irregular_form)

irregular_form  <- (irregular_form_kind ?ws forms) 
      | short_forms
      | plural_forms
      | personal_forms
      | past_forms
      | past_passive_participle_forms

past_passive_participle_forms <- (:'_прич. страд._' | :'_прич. страд. наст._') ?ws forms ?short_forms_in_parens

short_forms_in_parens <- :'(' short_forms :')'
short_forms <- :'_кф_' four_forms ?(?ws :'[//' ?style four_forms :']')
plural_forms <- :'_мн._' three_forms_variants
personal_forms <- :('_буд._' | '_наст._') (six_forms_variants | three_forms_variants | two_forms_variants)
past_forms <- :'_прош._' (four_forms_variants | three_forms_variants | two_forms_variants)

two_forms_variants <- two_forms ?(?ws :'[//' ?style two_forms :']')
three_forms_variants <- three_forms ?(?ws :'[//' ?style three_forms :']')
four_forms_variants <- four_forms ?(?ws :'[//' ?style four_forms :']')
six_forms_variants <- six_forms ?(?ws :'[//' ?style six_forms :']')

two_forms <- ?ws form
                comma forms

three_forms <- ?ws form
                comma forms
                comma forms

four_forms <- ?ws form
                comma forms
                comma forms
                comma forms

six_forms <- ?ws form
                comma forms
                comma forms
                comma forms
                comma forms
                comma forms

irregular_form_kind <- plural_genitive
                | genitive
                | short_masculine
                | past_passive_participle
                | present_first_person_singular
                | third_person_plural
                | comparative
                | imperative
                | gerund
                | present_participle

plural_genitive <- :'_Р. мн._'
genitive        <- :'_Р._'
short_masculine <- :'_кф м_'
past_passive_participle <- :'_прич. прош._'
present_first_person_singular <- :'_наст. 1 ед._'
third_person_plural <- :'_3 мн._'
comparative <- :'_сравн._'
imperative <- :'_повел._'
gerund     <- :'_деепр._'
present_participle <- :'_прич. наст._'

forms <= form *(?ws :'//' ?ws form)

form <- *(?ws style_possibly_in_parens) ?'-' headword ?('|' headword)

style_possibly_in_parens <= style_in_parens | style
style_in_parens <= :'(' style :')'
style <- (difficult | old | obsolete | folksy | prof | local | official | vernacular | poetic) ?ws

difficult <- :'_затрудн._'
old <- :'_устар._'
obsolete <- :'_устаревш._'
folksy <- :'_нар.-поэт._'
poetic <- :'_поэтич. также_' | :'_поэтич._'
prof <- :'_проф._' 
        | :'_у специалистов_'
        | :'_у музыкантов_'
        | :'_у моряков_'
        | :'_у химиков_'
        | :'_у медиков также_' 
        | :'_у медиков_'
local <- :'_местн._'
official <- :'_офиц._'
vernacular <- :'_простореч._'

stylistic_variant <- ?ws :'[' style ?(:'__' ?HomoNum headword :'__') def :']'

alt_entries <- ?ws :'[' ?:'//' alt_entry *(?ws :'//' alt_entry) ?ws ?:'=' :']'

alt_entry <- ?style ?:'__' ?HomoNum headword ?:'__' ?(ws def) ?see

see <- ?ws :'(_см._)'

comma <: ',' ?ws

extras <= extra *(comma extra)

extra <=  difficult_plural 
        | difficult_plural_genitive
        | difficult_comparative
        | difficult_short_feminine
        | difficult_short_masculine
        | difficult_short_except_masculine
        | difficult_imperative
        | difficult_first_person_singular
        | difficult_present_passive_participle
        | difficult_gerund
        | no_plural_genitive
        | no_short_masculine
        | no_full_forms
        | no_past_participle
        | no_future
        | has_passive
        | has_passive_participle
        | has_present_passive_participle
        | passive_participle_zhd
        | paragraph
        | sec_case
        | :'[' sec_case :']'
        | impersonal
        | repetitive

passive_participle_zhd <- :'_прич. страд._ -жд-'

sec_case <- [ПР] '2' ?preps

preps <- :'(' prep ?(:',' ?ws prep) :')'
prep <- 'в' | 'на'

paragraph <- :'§' Number

no_plural_genitive        <- :'_Р. мн. нет_'
no_short_masculine        <- :'_кф м нет_'
no_full_forms             <- :'_пф нет_'
no_future                 <- :'_буд. нет_' | :'_буд. в лит. языке нет_'
no_past_participle        <- :'_прич. прош. нет_'
has_passive               <- :'_имеется страд._'
has_passive_participle    <- :'_имеется прич. страд._'
has_present_passive_participle <- :'_имеется прич. страд. наст._' ?(?ws forms)
difficult_plural_genitive <- :'_Р. мн. затрудн._' | :'÷'
difficult_plural          <- :'_мн. затрудн._'
                           | :'_мн. избегается из-за совпадений с_ раб'
difficult_short_feminine  <- :'_кф ж затрудн._'
difficult_short_masculine <- :'_кф м затрудн._'
difficult_short_except_masculine <- :'_кф затрудн._ (_кроме кф м_)'
difficult_comparative     <- :'_сравн. затрудн._'
difficult_imperative      <- :'_повел. затрудн._'
difficult_gerund          <- :'_деепр. затрудн._'
difficult_first_person_singular <- :'_буд. 1 ед. затрудн._' 
                                 | :'_наст. 1 ед. затрудн._'
difficult_present_passive_participle <- :'_прич. страд. наст. затрудн._'
impersonal                <- :'безл.'
repetitive                <- :'многокр.'
yo <- :'ё' | 'о'

index_node <= angle_index | index0 | index

index0 <- :'0'

index <- index_number ?'°' ?'*' scheme
        ?:' ~ 0'
        ?letter_seq
        ?circledNumbers
        ?ws
        ?'⌧' ?'—' ?'✕' ?'~' ?'÷'
        ?(:'(' irregular_forms :')')
        ?(comma yo)

letter_seq <- ?ws :'(-' +LETTER :'-)' ?ws

circledNumbers <- +circledNumberOpt

circledNumberOpt <- circledNumber | circledNumberOptional

circledNumberOptional <- :'[' +circledNumber :']'

circledNumber <- [①②③④⑤⑥⑦⑧⑨]

angle_index <- (:'<' symbols ?comma ?ws index_variants :'>') | (symbol ?ws index)

symbols <= (symbol *(:'//' symbol)) | '§1' | '§2'

long_symbol <= interjection | conjunction | particle | predicative
        | pl_anim | pl_inan | plural
        | mo_plus | fo_plus
        | surname
        | adj_pron
        | ordinal
        | pronoun
        | compar
        | parenthetic
        | dual_aspect | perfective | imperfective
        | unisex | masc_anim | fem_anim
        | preposition
        | cardinal

symbol <- long_symbol
         | neut_anim | masc | fem | adj | adverb | neuter

interjection <- :'межд.'
conjunction  <- :'союз'
particle     <- :'част.'
predicative  <- :'предик.'
pl_anim      <- :'мн. одуш.'
pl_inan      <- :'мн. неод.'
plural       <- :'мн.'
mo_plus      <- :'мо⁺'
fo_plus      <- :'жо⁺'
surname      <- :'ф.'
adj_pron     <- :'мс-п'
ordinal      <- :'числ.-п'
pronoun      <- :'мс'
compar       <- :'сравн.'
parenthetic  <- :'вводн.'
dual_aspect  <- :'св-нсв'
perfective   <- :'св'
imperfective <- :'нсв'
unisex       <- :'мо-жо'
masc_anim    <- :'мо'
fem_anim     <- :'жо'
neut_anim    <- :'со'
preposition  <- :'предл.'
cardinal     <- :'числ.'
masc         <- :'м'
fem          <- :'ж'
neuter       <- :'с'
adj          <- :'п'
adverb       <- :'н'


index_number <= Number | '△'

scheme <- [a-f] ?"'" ?"'" ?slash_scheme

slash_scheme <- :'/' [a-c] ?"'" ?"'"

comment <: ?ws '(' !'_но ' !'_и ' !'△' !'_прош.' *(comment | !')' . ) ')'

ws <: +[ \t]

LETTER <= [а-яА-ЯёЁ]

STRESS_MARK <= [\u{0300}\u{0301}]

NewLine <: '\r\n' | '\n' | '\r'
