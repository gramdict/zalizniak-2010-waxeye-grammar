entry <- ?HomoNum headword ?StressInfo ?ws defs ?ws

StressInfo <- ?ws (NoStress | NormallyNoStress | OftenNoStress)

NoStress         <- :'(_без удар._)'
NormallyNoStress <- :'(_нормально без удар._)'
OftenNoStress    <- :'(_часто без удар._)'

defs <= def *(:';' ?ws def)

HomoNum <- ((Number '-' Number) | Number) :'/'

Number <= +[0-9]

headword <- (LETTER *(LETTER | STRESS_MARK | '-'))

plural_of <- ?ws :'_от_' ?ws
        ?(?HomoNum headword) ?ws (see | def)

def <- symbols ?' нп'
        ?plural_of
        ?alt_entries
        ?comment
        ?comma ?ws
        ?ref
        ?multiword_index
        ?comment
        ?comma
        ?extras
        ?stylistic_variant
        ?alt_entries
        ?comment
        ?irregular_forms
        ?alt_infinitive
        ?opposite_aspects
        ?comment
        ?idioms
        ?examples
        ?see_also
        ?alt_entries

alt_infinitive <- ?ws :'[_инф. также_' ?ws headword :']'

ref <-  ('_спряж. см._' | '_склон. см._')
        ?ws
        ?HomoNum
        headword
        ?ws ?comment
        ?comma
        ?'_но без нсв_'

opposite_aspects <- ?ws :'◑' opposite_aspect *(?ws +(((:'//') | (:';')) ?ws) ?style opposite_aspect)

opposite_aspect <- !'св' !'нсв' headword | ((Number | RomanNumber) *(:'//' (Number | RomanNumber)) ?comma ?opposite_aspect_word)

RomanNumber <- 'I' ?'I' ?'I'

opposite_aspect_word <- :'(' ?'-' headword :')'

see_also <- :';' ?ws :'_см. также' :?' отдельно' :'_' ?ws headword

examples <- :':' ?ws example *(:';' ?ws example)

example  <= +(LETTER | STRESS_MARK | [- (),_./—?] | '[' | ']' )

multiword_index <= index_variants *(plus index_variants)

index_variants <= index_node *(:'//' ?style index_node)

plus <: ?ws '+' ?ws 

idioms <- ?ws :'✧' ?ws idiom *(semicolon idiom)

idiom  <= !long_symbol +(LETTER | STRESS_MARK | [- (),_./—:] | '[' | ']' )

semicolon <: ';' ?ws

irregular_forms <- ?ws :'△' ?ws irregular_form *((comma | semicolon) irregular_form)

irregular_form  <- (case ?ws forms) 
      | short_forms
      | plural_forms
      | personal_forms
      | past_forms

short_forms <- :'_кф_' four_forms ?(?ws :'[//' ?style four_forms :']')
plural_forms <- :'_мн._' three_forms_variants
personal_forms <- :('_буд._' | '_наст._') three_forms_variants
past_forms <- :'_прош._' three_forms_variants

three_forms_variants <- three_forms ?(?ws :'[//' ?style three_forms :']')

first_form <- headword ?(:'|' headword)

three_forms <- ?ws first_form
                comma forms
                comma forms

four_forms <- ?ws first_form
                comma forms
                comma forms
                comma forms

case <- plural_genitive
      | genitive
      | short_masculine
      | passive_participle
      | present_first_person_singular
      | third_person_plural
      | comparative
      | imperative

plural_genitive <- :'_Р. мн._'
genitive        <- :'_Р._'
short_masculine <- :'_кф м_'
passive_participle <- :'_прич. страд._'
present_first_person_singular <- :'_наст. 1 ед._'
third_person_plural <- :'_3 мн._'
comparative <- :'_сравн._'
imperative <- :'_повел._'

forms <= form *(?ws :'//' ?ws form)

form <- ?style ?(:'(_затрудн._)' ?ws) ?'-' headword

style <- (old | obsolete | folksy | prof | local | official | vernacular | poetic) ?ws

old <- :'_устар._'
obsolete <- :'_устаревш._'
folksy <- :'_нар.-поэт._'
poetic <- :'_поэтич. также_' | :'_поэтич._'
prof <- :'_проф._' 
        | :'_у специалистов_'
        | :'_у музыкантов_'
        | :'_у моряков_'
        | :'_у химиков_'
        | :'_у медиков также_' 
        | :'_у медиков_'
local <- :'_местн._'
official <- :'_офиц._'
vernacular <- :'_простореч._'

stylistic_variant <- ?ws :'[' style ?(:'__' ?HomoNum headword :'__') def :']'

alt_entries <- ?ws :'[' ?:'//' alt_entry *(?ws :'//' alt_entry) ?ws ?:'=' :']'

alt_entry <- ?style ?:'__' ?HomoNum headword ?:'__' ?(ws def) ?see

see <- ?ws :'(_см._)'

comma <: ',' ?ws

extras <= extra *(comma extra)

extra <=  difficult_plural 
        | difficult_plural_genitive
        | difficult_comparative
        | difficult_short_feminine
        | difficult_short_masculine
        | difficult_short_except_masculine
        | difficult_imperative
        | difficult_first_person_singular
        | difficult_present_passive_participle
        | no_plural_genitive
        | no_short_masculine
        | no_full_forms
        | passive_participle_zhd
        | paragraph
        | sec_case
        | :'[' sec_case :']'
        | impersonal
        | repetitive

passive_participle_zhd <- :'_прич. страд._ -жд-'

sec_case <- [ПР] '2' ?preps

preps <- :'(' prep ?(:',' ?ws prep) :')'
prep <- 'в' | 'на'

paragraph <- :'§' Number

no_plural_genitive        <- :'_Р. мн. нет_'
no_short_masculine        <- :'_кф м нет_'
no_full_forms             <- :'_пф нет_'
difficult_plural_genitive <- :'_Р. мн. затрудн._' | :'÷'
difficult_plural          <- :'_мн. затрудн._'
                           | :'_мн. избегается из-за совпадений с_ раб'
difficult_short_feminine  <- :'_кф ж затрудн._'
difficult_short_masculine <- :'_кф м затрудн._'
difficult_short_except_masculine <- :'_кф затрудн._ (_кроме кф м_)'
difficult_comparative     <- :'_сравн. затрудн._'
difficult_imperative      <- :'_повел. затрудн._'
difficult_first_person_singular <- :'_буд. 1 ед. затрудн._' 
                                 | :'_наст. 1 ед. затрудн._'
difficult_present_passive_participle <- :'_прич. страд. наст. затрудн._'
impersonal                <- :'безл.'
repetitive                <- :'многокр.'
yo <- :'ё'

index_node <= angle_index | index0 | index

index0 <- :'0'

index <- index_number ?'°' ?'*' scheme
        ?:' ~ 0'
        ?letter_seq
        ?circledNumbers
        ?ws
        ?'⌧' ?'—' ?'✕' ?'~' ?'÷'
        ?(:'(' irregular_forms :')')
        ?(comma yo)

letter_seq <- ?ws :'(-' +LETTER :'-)' ?ws

circledNumbers <- +circledNumberOpt

circledNumberOpt <- circledNumber | circledNumberOptional

circledNumberOptional <- :'[' +circledNumber :']'

circledNumber <- [①②③④⑤⑥⑦⑧⑨]

angle_index <- (:'<' symbols ?comma ?ws index_variants :'>') | (symbol ?ws index)

symbols <= (symbol *(:'//' symbol)) | '§1' | '§2'

long_symbol <= 'межд.' | 'союз' | 'част.' | 'предик.' 
        | 'мн. одуш.' | 'мн. неод.' | 'мн.'
        | 'мо⁺' | 'жо⁺'
        | 'ф.'
        | 'мс-п'
        | 'числ.-п'
        | 'мс'
        | 'сравн.'
        | 'вводн.'
        | 'св-нсв' | 'св' | 'нсв' 
        | 'мо-жо' | 'мо' | 'жо'
        | 'предл.'
        | 'числ.'

symbol <- long_symbol
         | 'со' | 'м' | 'ж' | 'п' | 'н' | 'с'

index_number <= Number

scheme <- [a-f] ?"'" ?"'" ?slash_scheme

slash_scheme <- :'/' [a-c] ?"'" ?"'"

comment <: ?ws '(' !'_но ' !'_и ' !'△' !'_прош.' *(comment | !')' . ) ')'

ws <: +[ \t]

LETTER <= [а-яА-ЯёЁ]

STRESS_MARK <= [\u{0300}\u{0301}]

NewLine <: '\r\n' | '\n' | '\r'
